# Day-05

https://adventofcode.com/2022/day/7

## 上半截

题目第一眼看上去是要[模拟一个文件系统](https://twitter.com/clowwindy/status/1599568181668253697)，其实完全不是那么回事。

我的目标是吧输入处理成下面的样子：

|cmd           |enter |leave |size     |    |PathLevel|TotalSize|
|--------------|------|------|---------|----|---------|---------|
|$ cd /        | TRUE | FALSE| 0       |    | 1       | 48381165|
|$ ls          | FALSE| FALSE| 0       |    | -1      | 0       |
|dir a         | FALSE| FALSE| 0       |    | -1      | 0       |
|14848514 b.txt| FALSE| FALSE| 14848514|    | -1      | 0       |
|8504156 c.dat | FALSE| FALSE| 8504156 |    | -1      | 0       |
|dir d         | FALSE| FALSE| 0       |    | -1      | 0       |
|$ cd a        | TRUE | FALSE| 0       |    | 2       | 94853   |
|$ ls          | FALSE| FALSE| 0       |    | -2      | 0       |
|dir e         | FALSE| FALSE| 0       |    | -2      | 0       |
|29116 f       | FALSE| FALSE| 29116   |    | -2      | 0       |
|2557 g        | FALSE| FALSE| 2557    |    | -2      | 0       |
|62596 h.lst   | FALSE| FALSE| 62596   |    | -2      | 0       |
|$ cd e        | TRUE | FALSE| 0       |    | 3       | 584     |
|$ ls          | FALSE| FALSE| 0       |    | -3      | 0       |
|584 i         | FALSE| FALSE| 584     |    | -3      | 0       |
|$ cd ..       | FALSE| TRUE | 0       |    | 2       | 0       |
|$ cd ..       | FALSE| TRUE | 0       |    | 1       | 0       |
|$ cd d        | TRUE | FALSE| 0       |    | 2       | 24933642|
|$ ls          | FALSE| FALSE| 0       |    | -2      | 0       |
|4060174 j     | FALSE| FALSE| 4060174 |    | -2      | 0       |
|8033020 d.log | FALSE| FALSE| 8033020 |    | -2      | 0       |
|5626152 d.ext | FALSE| FALSE| 5626152 |    | -2      | 0       |
|7214296 k     | FALSE| FALSE| 7214296 |    | -2      | 0       |

Enter是说进入了新的目录，Leave就是`cd ..`，Size是这个文件的尺寸。

开始我是想在空着的那一列算当前的路径是哪里的。后来发现不用那么麻烦，输入是一个良好的深度优先遍历，我只要记录当前的深度就够了，逢Enter就+1，遇Leave就-1。

实际操作的时候，我还加个正负号，只有在Enter或者Leave的行才是正数，其他都是负数。这个操作主要是后面计算方便，非要说加个EnterOrLeave的列也不是不行……

最后是TotalSize的计算，懒得写Lambda了：
```
# 很明显有些内容是方便阅读的辅助线

=IF(       
 |-  B2,   # 只计算Enter的行
 |-  SUM(
 |   |  TAKE(
 |   |  |-  D3:D$24,   # D是size列，当前值到最后一行。
 |   |  |-  LET(
 |   |  |    tryFind,MATCH(F2-1,F3:F$24,0),   # 找到第一次退出到上一层
 |   |  |    IF(ISERROR(tryFind),24,tryFind)  # 因为深度遍历的时候，最右边的节点不会再回头往上走，最后的Level不会降成0. 
 |   |  |                                     # 所以需要提供一个最大行数做保底选项。
 |   |  |  )
 |   |  )
 |   ),
 |-  0
)
```

前面三列其实怎么做都行，我第一次写了三个公式，后来因为一个错误，重写了一遍。第二次直接一步到位：

```
=LET(
cmds,TEXTSPLIT(A2& "   "," ",,FALSE), # 这里偷偷在输入结尾添加了几个空格。因为有的输入只有2个指令，后面拿第三列的时候会报错。
isCD,IF(AND(CHOOSECOLS(cmds,1)="$",CHOOSECOLS(cmds,2)="cd"),TRUE,FALSE),
isEnter,IF(AND(isCD,CHOOSECOLS(cmds,3)<>".."),TRUE,FALSE),
isLeave,IF(AND(isCD,NOT(isEnter)),TRUE,FALSE),
fileSize,LET(sizeValue,VALUE(CHOOSECOLS(cmds,1)),IF(ISERROR(sizeValue),0,sizeValue)),
HSTACK(isEnter,isLeave,fileSize))
```

说回第一次做题时候遇到的问题。

一开始我想的是有一列是当前的路径，然后算当前目录内容总和的时候，先用StartWith来filter在当前目录下的所有文件，最后来求和。其实也没什么问题。
我也写了个StartWith的函数。

问题在于我用来Filter的时候，选的不是固定的输入行数`F2:F$24`，而是偷懒写了`F:F`，后来在实际跑输入的时候，直接把Excel卡死了。
重写的时候意识到了不需要StartWith那么复杂，就变成这样了。

## 下半截

下半截非常简单，找到满足条件的最小的某个目录的尺寸
```
=MIN(FILTER(G2:G24,G2:G24>8381165))
```

我提交了2次才过，主要是第一次算那个`8381165`算错了……
