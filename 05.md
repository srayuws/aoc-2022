# Day-05

https://adventofcode.com/2022/day/5

## 上半截

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
```
题目很好理解，例子里是3个stack，然后按照下面的指示在几个stack上搬运箱子。
当一次搬运多个箱子的时候，每次搬一个，重复多次。所以搬运3个的时候，3个箱子的上下顺序会颠倒一次。

还是题目中的例子

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1

[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
 
move 3 from 1 to 3

        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

最后问搬运完以后，每个stack的顶上的箱子是哪个。

做题思路是输出这样的一个Excel表格

| 1   | 2   |    3 |   | Move | From | To |
|-----|-----|-----:|---|------|------|----|
| NZ  | DCM |    P |   |      |      |    |
| DNZ | CM  |    P |   | 1    | 2    | 1  |
|     | CM  | ZNDP |   | 3    | 1    | 3  |
| MC  |     | ZNDP |   | 2    | 2    | 1  |
| C   | M   | ZNDP |   | 1    | 1    | 2  |

每个stack表达成一个text，然后第一个字符是栈顶。

做题的时候，先手动抄一下初始状态。
右边的 Move/From/To 其实也可以直接复制了以后用excel公式来生成。我偷懒了，复制到文本编辑器里，预处理成了csv，然后导入数据的。

实际做题的公式如下：

```
Solve
=LAMBDA(
    # 输入分别是：移动前的状态表（1*N），from/to/move的值
    previous, from, to, count,                 
    LET(
        fromCell, CHOOSECOLS(previous,from) ,        # CHOOSECOLS 可以从表里抽出指定的列
        toCell, CHOOSECOLS(previous, to),
        newTo, MoveChar(fromCell, toCell, count) ,   # 把顶上的n个箱子搬到新的栈上
        newFrom , RemoveChar(fromCell, count) ,      # 移除老栈顶上的n个箱子
        output, ReplaceX(previous, newFrom, newTo, from, to), # 把搬运以后的数据拼装起来
        output
    )
)

MoveChar
=LAMBDA(
    fromCell,toCell,count, 
    IF(count=0,
        toCell,  # n=0 时，递归终止
        LET(
            topChar, LEFT(fromCell, 1),
            MoveChar(RemoveChar(fromCell,1), topChar & toCell, count-1) # 每次搬一个箱子，递归
        )
    )
)

RemoveChar
=LAMBDA( # 直接移除最左侧的n个字符/箱子
    textCell, count,
    RIGHT(textCell,LEN(textCell)-count)
)

ReplaceX # 因为Replace函数已经被系统用过了，结尾随便加了个X
=LAMBDA(
    previous, newFrom, newTo, from, to, 
    LET(
        # 因为from/to的左右顺序不一定，不太好拼装。所以这里先分清左右列，然后替换掉原表格里的列
        colNumberSmall, MIN(from, to),
        colNumberLarge, MAX(from,to),
        colSmall, IF(from=colNumberSmall, newFrom, newTo),
        colLarge, IF(from=colNumberLarge, newFrom, newTo),
        ReplaceImpl(previous,colNumberSmall,colNumberLarge,colSmall,colLarge)
    ) 
)


ReplaceImpl 
=LAMBDA(
    previous,colNumberSmall,colNumberLarge,colSmall,colLarge,
    LET(
        # 实际的拼装过程
        # 因为Excel里不支持空行/空列。所以需要靠几个IF来处理不同的情况
        #
        # 3个IF分表处理几种不同情况：
        #   [===] 表示不需要动的列
        #   (X) 表示需要替换的列
        #
        # IF-1, 左侧顶格
        #   [===] (X) [===] (X) [===]
        #   (X) [===] (X) [===]
        # IF-2， 右侧顶格
        #   [===] (X) [===] (X) [===]
        #   [===] (X) [===] (X)
        # IF-3， 左右相邻
        #   [===] (X) [===] (X) [===]
        #   [===] (X) (X) [===]
        leftPart, IF(colNumberSmall=1, colSmall, HSTACK(TAKE(previous,1,colNumberSmall-1),colSmall)), # IF-1
        midPart, TAKE(DROP(previous,0,colNumberSmall), 1, colNumberLarge-colNumberSmall -1),
        rightPart, IF(colNumberLarge=COLUMNS(previous), colLarge, HSTACK(colLarge,DROP(previous,0,colNumberLarge)) ), # IF-2
        IF(colNumberLarge-colNumberSmall=1 , # IF-3
           HSTACK(leftPart,rightPart), 
           HSTACK(leftPart,midPart,rightPart))
    )
)

```

最后提交的时候，因为中间有一列太长，没看清，少抄了一列，错了一次。

## 下半截

跟上半截唯一的区别是，一次搬运多个箱子的时候，几个箱子的上下顺序不变。
只要改一个函数的实现就可以。

```
MoveChar
=lambda(
    fromCell, toCell, count,
    LET(
        word, LEFT(fromCell, count),
        word & toCell
    )
) 
```
