# Day-08

https://adventofcode.com/2022/day/8

## 上半截

开始先把输入数据里的一行数字变成一行n个格子，如下：
（我在最上面和最左边加了一组index，方便后续找格子和调试）

|   | 1 | 2 | 3 | 4 | 5 |
|---|---|---|---|---|---| 
| 1 | 3 | 0 | 3 | 7 | 3 | 
| 2 | 2 | 5 | 5 | 1 | 2 | 
| 3 | 6 | 5 | 3 | 3 | 2 | 
| 4 | 3 | 3 | 5 | 4 | 9 | 
| 5 | 3 | 5 | 3 | 9 | 0 | 

代码：
```
toLine
=lambda(
    input,
    if(len(input)=1,
        value(input),
        HSTACK(  
            value(LEFT(input,1)),
            toLine(right(input,len(input)-1),len(input-1))) 
    )
)
```

实际问题输入的时候，因为数字太长，被Excel默认用科学计数法处理了。我在编辑器里批量的把所有`12345`都替换成了`="12345"`。

然后定义了几个处理中间过程的函数：

```
ChoosePosition # 从输入的二维矩阵里，根据行列编号取数
=lambda(input,row,col,
  chooseRows(chooseCols(input,col),row)
)

Reverse # 把一列倒转一下
=lambda(
    input,
    if(
        rows(input)=1, 
        input,
        vstack(take(input,-1), Reverse(take(input,rows(input)-1)))
    )
)
```

然后是实际处理问题的函数：
```
LookForward # 从tree的高度向着others的方向望过去是否，能看到头
=lambda(
    tree, others,
    let(
        first, take(others,1),        
        if(
            rows(others)=1,
            tree>first,
            if(tree<=first, FALSE, LookForward(tree, take(others, 1-rows(others))))
        )
    )
)

isVisible
=lambda(
    treeMap, treeRow, treeCol,
    let(
        totalRows, Rows(treeMap),
        totalCols, Columns(treeMap),
        currentTree, ChoosePosition(treeMap,treeRow,treeCol) , 
        visibleTop, LookForward(currentTree, Reverse(take(chooseCols(treeMap,treeCol), treeRow-1)) ),
        visibleBottom, LookForward(currentTree, take(chooseCols(treeMap,treeCol), treeRow-totalRows) ),
        visibleLeft, LookForward(currentTree, Reverse(TRANSPOSE(take(chooseRows(treeMap,treeRow), , treeCol-1)))),
        visibleRight, LookForward(currentTree, TRANSPOSE(take(chooseRows(treeMap,treeRow), ,treeCol-totalCols))),
        if(OR(visibleTop,visibleBottom,visibleLeft,visibleRight ),1,0) #只要有一条边能看过去，就是1
    )
)
```

最后做出来的表类似这样：

| 1 | 2 | 3 | 4 | 5 |   | 
|---|---|---|---|---|---| 
| 1 | 3 | 0 | 3 | 7 | 3 | 
| 2 | 2 | 5 | 5 | 1 | 2 | 
| 3 | 6 | 5 | 3 | 3 | 2 | 
| 4 | 3 | 3 | 5 | 4 | 9 | 
| 5 | 3 | 5 | 3 | 9 | 0 | 
|   |   |   |   |   |   | 
|   |   |   |   |   |   | 
|   | 1 | 2 | 3 | 4 | 5 | 
| 1 |   |   |   |   |   | 
| 2 |   | 1 | 1 | 0 |   | 
| 3 |   | 1 | 0 | 1 |   | 
| 4 |   | 0 | 1 | 0 |   | 
| 5 |   |   |   |   |   | 

其中下表的左上角的`1`的公式类似于`=IsVisible($B$8:$F$12,$A17,C$15)`，其中`B8:F12`是第一张5x5的表的坐标。

最后再用Sum求下表的总和，加上边缘的格子数量。

## 下半截

只需要稍微修改一下 LookForward 和 IsVisible 的定义就行。太直观了，懒得抄了……
